import java.text.SimpleDateFormat

def date = new Date()

def buildTimeDateFormat = new SimpleDateFormat("yyyy-MM-dd HH:mm:ss'Z'")
buildTimeDateFormat.setTimeZone(TimeZone.getTimeZone("UTC"))
def buildTime = buildTimeDateFormat.format(date)

def majorVersion = 0
def minorVersion = 2
def minorMinorVersion = 0
def javaVersion = 1.8

apply plugin: 'base'
apply plugin: 'java'

clean.doFirst {
    delete "${projectDir}/build"
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'

    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion

    def vendor = 'Liquorice.io'

    group = 'io.liquorice.config'
    version = "${majorVersion}.${minorVersion}.${minorMinorVersion}"

    jar.doFirst {
        manifest {
            attributes(
                    'Manifest-Version'			: '1.0',
                    'Created-By'				: vendor,
                    'Specification-Title'		: project.name,
                    'Specification-Version'		: version,
                    'Specification-Vendor'		: vendor,
                    'Implementation-Title'		: project.name,
                    'Implementation-Version'	: version,
                    'Implementation-Vendor'		: vendor,
                    'Class-Path'				: configurations.compile.collect { it.getName() }.join(' '),
                    'Build-Name'				: "${group}:${project.name}:?:${version}",
                    'Build-Time'				: buildTime
            )
        }
    }

    sourceSets {
        integrationTest {
            java.srcDir file('src/integration-test/java')
            resources.srcDir file('src/integration-test/resources')
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        testCompile group: 'org.testng', name: 'testng', version: '6.9.4'
        testCompile group: 'org.mockito', name: 'mockito-core', version: '2.2.26'

        integrationTestCompile sourceSets.main.output
        integrationTestCompile configurations.testCompile
        integrationTestRuntime sourceSets.test.output
        integrationTestRuntime configurations.testRuntime
    }

    // Unit testing
    test {
        useTestNG()
    }

    // Integration testing
    configurations {
        integrationTestCompile.extendsFrom testCompile
        integrationTestRuntime.extendsFrom testRuntime
    }

    task integrationTest(type: Test) {
        testClassesDir = sourceSets.integrationTest.output.classesDir
        classpath = sourceSets.integrationTest.runtimeClasspath
        mustRunAfter test
        useTestNG()
    }
    check.dependsOn integrationTest

    // Reporting
    tasks.withType(Test) {
        reports.html.destination = file("${reporting.baseDir}/${name}")
    }
}

apply from: '${projectDir}/../config/gradle/checkstyle.gradle'
apply from: '${projectDir}/../config/gradle/findbugs.gradle'
// Uncomment to enable local code coverage reporting
//apply from: '${projectDir}/../config/gradle/jacoco.gradle'
apply from: '${projectDir}/../config/gradle/javadocs.gradle'
apply from: '${projectDir}/../config/gradle/pmd.gradle'
